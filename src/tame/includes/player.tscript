
/*****************************************************************************
 * DOOMGUY!
 ****************************************************************************/
extend player p_doomguy
{
	init()
	{
		health = 100;
		berserk = false;
		godmode = false;
		cheater = false;
		
		// equipped weapon
		equippedWeapon = false;
		equippedWeaponName = false;
		equippedWeaponSound = false;
		equippedAttackDice = 0;
		equippedAttackDiceSize = 0;
	}
	
	function doBadAction()
	{
		local x = irandom(3);
		if (x == 0)
			textln("I don't understand.");
		else if (x == 1)
			textln("I don't know what you mean.");
		else if (x == 2)
			textln("I'm not sure I understand.");
	}
	
	onAction(a_checkdeath)
	{
		if (health > 0)
			end;
		textln("You are dead.");
		queue a_gameover;
	}

	onAction(a_yell)
	{
		textln("In space, no one can hear you scream!");
	}

	onAction(a_inventory)
	{
		if (objectCount(player) == 0)
			textln("You don't have anything.");
		else
		{
			textln("You examine your inventory.");
			Browse(player);
		}

		if (equippedWeapon)
			textln("\nYou have the "+equippedWeaponName+" equipped as your current weapon.");
		textln("");
		queue a_status;
	}
	
	onAction(a_status)
	{
		text("Your health is at "+health+"% - ");
		if (health <= 25)
			textln("You're probably gonna die soon without aid.");
		else if (health <= 50)
			textln("Not good. Gotta find some medicine.");
		else if (health <= 75)
			textln("You could probably do better.");
		else if (health < 100)
			textln("Just a scratch or two.");
		else if (health == 100)
			textln("As fit as a fiddle!");
		else
			textln("You feel AMAZING!");
		if (berserk)
			textln("You are also BERZERK! HIT SOMEBODY!");
	}

	onAction(a_die)
	{
		textln("You decide to end it all.");
		health = 0;
	}
	
	onAmbiguousCommand()
	{
		textln("There are a lot of things here that are called that. Be more specific.");
	}
	
	onIncompleteCommand(a_examine)
	{
		textln("WHAT should you EXAMINE (or LOOK AT)?");
	}

	onIncompleteCommand(a_use)
	{
		textln("WHAT should you USE (and maybe WITH something)?");
	}

	onIncompleteCommand(a_shoot)
	{
		textln("WHAT should you SHOOT?");
	}

	onIncompleteCommand(a_take)
	{
		textln("WHAT should you TAKE?");
	}

	onIncompleteCommand(a_punch)
	{
		textln("WHAT should you SHOOT?");
	}

	onIncompleteCommand()
	{
		textln("You should be more specific.");
	}

	onUnhandledAction(a_close)
	{
		textln("You can't close that.");
	}

	onUnhandledAction(a_open)
	{
		textln("Try as you might, you can't open it.");
	}

	onUnhandledAction(a_shoot)
	{
		textln("Not a good idea to shoot that.");
	}

	onUnhandledAction(a_take)
	{
		textln("You can't take that.");
	}

	onUnhandledAction(a_use)
	{
		textln("You can't USE that, but you might be able to do other things with it.");
	}

	onUnhandledAction()
	{
		textln("You can't do that.");
	}

	onMalformedCommand()
	{
		doBadAction();
	}

	onUnknownCommand()
	{
		doBadAction();
	}

}
