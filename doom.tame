//************************************************
// Doom: The Text Adventure.
// Main
//************************************************

module
{
	title = "Doom: The Text Adventure";
	author = "Matt Tropiano";
	email = "mtropiano@gmail.com";
	twitter = "@TheRealMTrop";
	website = "http://mtrop.net";
	country = "us";
	language = "enu";
	version = "1.0";
}

#include "includes/actions.tame"
#include "includes/prototypes.tame"
#include "includes/objects.tame"
#include "includes/rooms.tame"
#include "includes/player.tame"

world
{
	AfterModuleInit()
	{
		// world setup.
		SetPlayer(p_doomguy);
		SetRoom(p_doomguy, r_entryway);
		// add surrogates.
		GiveObject(world, o_surroundings);
		// Set up objects.
		GiveObject(p_doomguy, o_pistol);
		GiveObject(p_doomguy, o_self);
		o_pistol.equip();
		
		textln("The nightmare begins.\n");
		QueueAction(a_whereami);
		QueueAction(a_description);
	}
	
	AfterRequest()
	{
		// Call "think" on monsters.
		QueueActionForTaggedObjectsIn(a_monsterthink, room, "monster");
		QueueAction(a_checkdeath);
	}
	
	OnAction(a_quit)
	{
		local x = random(4);
		if (x == 0)
			textln("Let's beat it - this is turning into a novel!");
		else if (x == 1)
			textln("Please don't leave, there's more demons to type to death!");
		else if (x == 2)
			textln("You're trying to say you like graphics better than me, right?");
		else if (x == 3)
			textln("Go ahead and leave. See if I care.");
		quit;
	}

	OnAction(a_gameover)
	{
		textln("Game Over! You lose.");
		quit;
	}
	
}

