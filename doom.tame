//************************************************
// Doom: The Text Adventure.
// Main
//************************************************

module
{
	title = "Doom: The Text Adventure";
	author = "Matt Tropiano";
	email = "mtropiano@gmail.com";
	twitter = "@TheRealMTrop";
	website = "http://mtrop.net";
	origin_locale = "en_US";
	origin_language = "eng";
	version = "1.0";
}

#include "includes/actions.tame"
#include "includes/prototypes.tame"
#include "includes/objects.tame"
#include "includes/rooms.tame"
#include "includes/player.tame"

world
{
	init()
	{
		textln("The nightmare begins.\n");
	}

	afterModuleInit()
	{
		// add surrogates.
		giveObject(world, o_surroundings);
		// world setup.
		setPlayer(p_doomguy);
		setRoom(p_doomguy, r_entryway);
		queueAction(a_whereami);
		queueAction(a_description);
	}
	
	afterRequest()
	{
		queueAction(a_think);
	}
	
	onAction(a_quit)
	{
		local x = random(4);
		if (x == 0)
			textln("Let's beat it - this is turning into a novel!");
		else if (x == 1)
			textln("Please don't leave, there's more demons to type to death!");
		else if (x == 2)
			textln("You're trying to say you like graphics better than me, right?");
		else if (x == 3)
			textln("Go ahead and leave. See if I care.");
		quit;
	}

	onAction(a_think)
	{
		// Call "think" on monsters.
		queueActionForTaggedObjectsIn(a_monsterthink, room, "monster");
		queueAction(a_checkdeath);
	}

	onAction(a_gameover)
	{
		textln("Game Over! You lose.");
		quit;
	}
	
}

