// Add to world - surrogate for "look" with "examine"
object o_surroundings named "surroundings", "room" {}
// Add to world. Surrogate for "Status" with "examine"
object o_self named "self", "myself", "doomguy" {}

object o_pistol
{
	onPlayerBrowse()
	{
		textln("A pistol. Your trusty sidearm. Standard issue.");
	}
}

object o_reddoor0 named 
	"door", "red door", 
	"door with red markings", "door with the red markings"
{
	init()
	{
		browsed = false;
		examined = false;
		firstuse = false;
		unlocked = false;
	}

	onAction(a_examine)
	{
		if(!examined)
		{
			textln("A very heavy steel door with red markings flanking the tracks.");
			textln("A sign on the door reads, \"Security Clearance Required\".");
			textln("Another sign above the door reads, \"Main Hall\".");
			addObjectName(o_reddoor0, "security door");
			addObjectName(o_reddoor0, "main hall door");
			addObjectName(o_reddoor0, "door to main hall");
			addObjectName(o_reddoor0, "door to the main hall");
		}
		else
		{
			textln("It's the red door to the Main Hall.");
			textln("A sign on the door reads, \"Security Clearance Required\".");
		}
		examined = true;
	}

	onAction(a_take)
	{
		textln("It's a very big, heavy door. You can carry almost anything, but this probably won't fit in your pockets.");
	}

	onAction(a_close)
	{
		textln("It's already closed. Good job!");
	}
	
	onAction(a_open)
	{
		if (unlocked)
		{
			setRoom(r_mainhall);
			queueAction(a_whereami);
			queueAction(a_description);
		}
		else if (!playerHasObject(player, o_redkey))
		{
			if (!firstuse)
				textln("It's locked. It looks like I need a keycard of some kind.");
			else
				textln("It's locked.");
			firstuse = true;
		}
		else
		{
			queueActionObject2(a_use, o_reddoor0, o_redkey);
		}
	}
	
	onAction(a_use)
	{
		queueAction(a_open);
	}

	onActionWith(a_use, o_redkey)
	{
		if (!unlocked)
		{
			textln("You shove the keycard in the door. It beeps with a happy-sounding beep.");
			textln("You hear locks disengage at the bottom of the door.");
		}
		else
			textln("It's already unlocked.");
		unlocked = true;
	}
	
	onRoomBrowse()
	{
		if(!browsed)
			textln("There's a door here, lined with red markings.");
		else if (!examined)
			textln("There's a red door here.");
		else
			textln("The red door to the main hall is here.");
	}
	
}