
// ==========================
// ALL OBJECTS
// ==========================
extend object o_object
{
	init()
	{
		examined = false;
	}
	
	// Called on first examine.
	function examineFirst()
	{
		/* Do nothing. Override this. */
	}
	
	// Called on subsequent examines.
	function examineNext()
	{
		/* Do nothing. Override this. */
	}
	
	// Called on every examine (after first/next).
	function examineAlways()
	{
		/* Do nothing. Override this. */
	}
	
	function takeMe()
	{
		textln("Taken.");
	}

	onAction(a_take)
	{
		if (hasObject(player, this))
			textln("You already have this.");
		else
		{
			takeMe();
			giveObject(player, this);
		}
	}

	onAction(a_examine)
	{
		if (!examined)
		{
			examineFirst();
			examined = true;
		}
		else
		{
			examineNext();
		}
		
		examineAlways();
	}

	onAction(a_equip)
	{
		textln("You can't equip that. It's not a weapon!");
	}
	
	onAction(a_shoot)
	{
		textln("Not a good idea to shoot that.");
	}
}


// ==========================
// Proxy
// ==========================

// Add to player - surrogate for "look" with "examine"
object o_surroundings named "surroundings", "room" uses determiners "my", "the"
{
	onAction(a_examine)
	{
		queue a_look;
	}
}

// Add to player. Surrogate for "Status" with "examine"
object o_self named "self", "myself", "doomguy", "me"
{
	onAction(a_examine)
	{
		queue a_status;
	}

	onAction(a_shoot)
	{
		textln("Hmm. Maybe not.");
	}

	onAction(a_equip)
	{
		textln("You already have fists. DUH!");
	}
	
}

#include "objects/items.tame"
#include "objects/weapons.tame"
#include "objects/monsters.tame"
#include "objects/doors.tame"
