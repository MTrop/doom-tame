
// ==========================
// ALL OBJECTS
// ==========================
object archetype o_allobjects
{
	init()
	{
		examined = false;
	}
	
	// Called on first examine.
	procedure("ExamineFirst")
	{
		/* Do nothing. Override this. */
	}
	
	// Called on subsequent examines.
	procedure("ExamineNext")
	{
		/* Do nothing. Override this. */
	}
	
	// Called on every examine (after first/next).
	procedure("ExamineAlways")
	{
		/* Do nothing. Override this. */
	}
	
	onAction(a_examine)
	{
		if (!examined)
		{
			call("ExamineFirst");
			examined = true;
		}
		else
		{
			call("ExamineNext");
		}
		
		call("ExamineAlways");
	}

}


// ==========================
// Proxy
// ==========================

// Add to player - surrogate for "look" with "examine"
object o_surroundings named "surroundings", "room" uses determiners "my", "the"
{
	onAction(a_examine)
	{
		queueAction(a_look);
	}
}

// Add to player. Surrogate for "Status" with "examine"
object o_self named "self", "myself", "doomguy", "me"
{
	onAction(a_examine)
	{
		queueAction(a_status);
	}

	onAction(a_shoot)
	{
		textln("Hmm. Maybe not.");
	}
	
}

#include "objects/items.tame"
#include "objects/weapons.tame"
#include "objects/monsters.tame"
#include "objects/doors.tame"
