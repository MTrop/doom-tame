
// ==========================
// Doors
// ==========================
extend object o_door
{
	// Should move player to connected room.
	function goToNextRoom()
	{
		/* Nothing. Override this. */
		addCue("info", "THIS DOOR LEADS NOWHERE. OH NO.");
	}
	
	override onAction(a_take)
	{
		textln("It's a heavy door. You can carry almost anything, but this probably won't fit in your pockets.");
	}
	
	onAction(a_use)
	{
		queue a_open, this;
	}

	onAction(a_open)
	{
		goToNextRoom();
	}

	onAction(a_close)
	{
		textln("It's already closed. Good job!");
	}
	
	onActionWithOther(a_use)
	{
		textln("That doesn't look like it'll work.");
	}

}

extend object o_lockeddoor
{
	override init()
	{
		examined = false;
		locked = true;
		tryopen = 0;
	}
	
	// Called to alert the player that it's locked.
	function lockMessage()
	{
		if (tryopen == 0)
			textln("It's locked. I need a key.");
		else if (tryopen == 1)
			textln("It's still locked.");
		else if (tryopen == 2)
			textln("You try to dig your fingers under the door lip and lift. No dice. Still locked.");
		else if (tryopen == 3)
			textln("It's really, really locked.");
		else if (tryopen == 4)
			textln("It's so TOTALLY locked.");
		else if (tryopen == 5)
			textln("It's absolutely locked.");
		else if (tryopen == 6)
			textln("OH MY GOD. IT'S LOCKED, OKAY?! FIND A DAMN KEY.");
		else
			textln("It's locked.");
		
		tryopen = tryopen + 1;
	}
	
	override onAction(a_open)
	{
		if (locked)
			lockMessage();
		else
			goToNextRoom();
	}
}

// Red Door, to Main Hall
extend object o_reddoor0
{
	override init()
	{
		examined = false;
		locked = true;
	}

	override function examineFirst()
	{
		textln("A very heavy steel door with red markings flanking the tracks.");
		textln("A sign on the door reads, \"Security Clearance Required\".");
		textln("Another sign above the door reads, \"Main Hall\".");
		addObjectName(o_reddoor0, "security door");
		addObjectName(o_reddoor0, "main hall door");
		addObjectName(o_reddoor0, "door to main hall");
		addObjectName(o_reddoor0, "door to the main hall");
	}

	override function examineNext()
	{
		textln("It's the red door to the Main Hall.");
		textln("A sign on the door reads, \"Security Clearance Required\".");
	}

	override function goToNextRoom()
	{
		setRoom(player, r_mainhall);
		queue a_description;
	}
	
	override onActionWithOther(a_use)
	{
		textln("That doesn't look like it'll work. You'll need a matching key of some sort.");
	}
	
	onActionWith(a_use, o_redkey)
	{
		if (!locked)
		{
			textln("You shove the keycard in the door. Nothing happens.");
			textln("Probably because it's already unlocked.");
		}
		else
		{
			textln("You shove the red keycard in the door. It beeps with a happy-sounding beep.");
			textln("You hear locks disengage at the bottom of the door.");
			locked = false;
		}
	}
	
	onRoomBrowse()
	{
		if (!examined)
			textln("There's a door here, lined with red markings.");
		else
			textln("The red door to the main hall is here.");
	}
	
}

// Red Door, to Entryway
extend object o_reddoor1
{
	override init()
	{
		examined = true;
		locked = false;
		addObjectName(o_reddoor1, "security door");
		addObjectName(o_reddoor1, "entryway door");
		addObjectName(o_reddoor1, "door to entryway");
		addObjectName(o_reddoor1, "door to the entryway");
	}

	override function examineAlways()
	{
		textln("It's the red door to the Entryway.");
	}

	override function goToNextRoom()
	{
		setRoom(player, r_entryway);
		queue a_description;
	}
	
	onRoomBrowse()
	{
		textln("The red door to the main hall is here.");
	}
	
}

// Door to supply room.
extend object o_supplydoor0
{
	override function goToNextRoom()
	{
		examined = true;
		setRoom(player, r_supplyroom);
		queue a_description;
	}
	
	override function examineFirst()
	{
		textln("A small, featureless door with a plate that says \"PUSH\".");
		textln("A sign on the door reads, \"Supply Room\".");
		addObjectName(o_supplydoor0, "supply room door");
		addObjectName(o_supplydoor0, "door to supply room");
		addObjectName(o_supplydoor0, "door to the supply room");
	}

	override function examineNext()
	{
		textln("It's the door to the Supply Room.");
	}

	onRoomBrowse()
	{
		if (!examined)
			textln("A small door is to the left.");
		else
			textln("The door to the supply room is here.");
	}
}

// Door to entryway via supply room.
extend object o_supplydoor1
{
	override function goToNextRoom()
	{
		examined = true;
		setRoom(player, r_entryway);
		queue a_description;
	}

	override function examineAlways()
	{
		textln("It's the door to the Entryway. You came through here.");
	}

	onRoomBrowse()
	{
		textln("The small door back to the entryway is here.");
	}

}
