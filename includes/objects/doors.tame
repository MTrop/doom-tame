
// ==========================
// Doors
// ==========================
object archetype o_everydoor
{
	init()
	{
		examined = false;
		locked = false;
		tryopen = 0;
	}
	
	// Should move player to connected room.
	function goToNextRoom()
	{
		/* Nothing. Override this. */
		info("THIS DOOR LEADS NOWHERE. OH NO.");
	}
	
	// Should attempt to unlock the door. Set "locked" variable.
	function tryUnlock()
	{
		/* Nothing. Override this. Try to detect necessary key on player. */
	}
	
	// Called to alert the player that it's locked.
	function isLocked()
	{
		if (tryopen == 0)
			textln("It's locked. I need a key.");
		else if (tryopen == 1)
			textln("It's still locked.");
		else if (tryopen == 2)
			textln("You try to dig your fingers under the door lip and lift. No dice. Still locked.");
		else if (tryopen == 3)
			textln("It's really, really locked.");
		else if (tryopen == 4)
			textln("It's so TOTALLY locked.");
		else if (tryopen == 5)
			textln("It's absolutely locked.");
		else if (tryopen == 6)
			textln("OH MY GOD. IT'S LOCKED, OKAY?! FIND A DAMN KEY.");
		else
			textln("It's locked.");
		
		tryopen = tryopen + 1;
	}
	
	onAction(a_take)
	{
		textln("It's a heavy door. You can carry almost anything, but this probably won't fit in your pockets.");
	}
	
	onAction(a_open)
	{
		if (locked)
			TryUnlock();
		
		if (locked)
			IsLocked();
		else
			GoToNextRoom();
	}

	onAction(a_close)
	{
		textln("It's already closed. Good job!");
	}
	
	onActionWithOther(a_use)
	{
		textln("That doesn't look like it'll work.");
	}

}

// Red Door, to Main Hall
object o_reddoor0 named "door", "red door", "door with red markings", "door with the red markings" uses determiners "the"
{
	init()
	{
		examined = false;
		locked = true;
	}

	function examineFirst()
	{
		textln("A very heavy steel door with red markings flanking the tracks.");
		textln("A sign on the door reads, \"Security Clearance Required\".");
		textln("Another sign above the door reads, \"Main Hall\".");
		addObjectName(o_reddoor0, "security door");
		addObjectName(o_reddoor0, "main hall door");
		addObjectName(o_reddoor0, "door to main hall");
		addObjectName(o_reddoor0, "door to the main hall");
	}

	function examineNext()
	{
		textln("It's the red door to the Main Hall.");
		textln("A sign on the door reads, \"Security Clearance Required\".");
	}

	function tryUnlock()
	{
		if (hasObject(player, o_redkey))
		{
			textln("You shove the keycard in the door. It beeps with a happy-sounding beep.");
			textln("You hear locks disengage at the bottom of the door.");
			locked = false;
		}
	}

	function goToNextRoom()
	{
		setRoom(player, r_mainhall);
		queueAction(a_whereami);
		queueAction(a_description);
	}
	
	onAction(a_use)
	{
		queueActionObject(a_open, o_reddoor0);
	}

	onActionWith(a_use, o_redkey)
	{
		if (!locked)
		{
			textln("You shove the keycard in the door. Nothing happens.");
			textln("Probably because it's already unlocked.");
		}
		else
			TryUnlock();
	}
	
	onActionWithOther(a_use)
	{
		textln("That doesn't look like it'll work. You'll need a matching key of some sort.");
	}
	
	onRoomBrowse()
	{
		if (!examined)
			textln("There's a door here, lined with red markings.");
		else
			textln("The red door to the main hall is here.");
	}
	
}

// Red Door, to Entryway
object o_reddoor1 named "door", "red door", "door with red markings", "door with the red markings" uses determiners "the"
{
	init()
	{
		examined = true;
		locked = false;
		addObjectName(o_reddoor1, "security door");
		addObjectName(o_reddoor1, "entryway door");
		addObjectName(o_reddoor1, "door to entryway");
		addObjectName(o_reddoor1, "door to the entryway");
	}

	function examineAlways()
	{
		textln("It's the red door to the Entryway.");
	}

	function goToNextRoom()
	{
		setRoom(player, r_entryway);
		queueAction(a_whereami);
		queueAction(a_description);
	}
	
	onAction(a_use)
	{
		queueActionObject(a_open, o_reddoor1);
	}

	onRoomBrowse()
	{
		textln("The red door to the main hall is here.");
	}
	
}

// Door to supply room.
object o_supplydoor0 named "door", "small door" uses determiners "the"
{
	function goToNextRoom()
	{
		examined = true;
		setRoom(player, r_supplyroom);
		queueAction(a_whereami);
		queueAction(a_description);
	}
	
	function examineFirst()
	{
		textln("A small, featureless door with a plate that says \"PUSH\".");
		textln("A sign on the door reads, \"Supply Room\".");
		addObjectName(o_supplydoor0, "supply room door");
		addObjectName(o_supplydoor0, "door to supply room");
		addObjectName(o_supplydoor0, "door to the supply room");
	}

	function examineNext()
	{
		textln("It's the door to the Supply Room.");
	}

	onRoomBrowse()
	{
		if (!examined)
			textln("A small door is to the left.");
		else
			textln("The door to the supply room is here.");
	}
}

// Door to entryway via supply room.
object o_supplydoor1 named "door", "small door", "entryway door", "door to entryway", "door to the entryway" uses determiners "the"
{
	function goToNextRoom()
	{
		examined = true;
		setRoom(player, r_entryway);
		queueAction(a_whereami);
		queueAction(a_description);
	}

	function examineAlways()
	{
		textln("It's the door to the Entryway. You came through here.");
	}

	onRoomBrowse()
	{
		textln("The small door back to the entryway is here.");
	}

}
