
// ==========================
// Weapons
// ==========================

object archetype o_allweapons : o_allobjects
{
	procedure("EquipMe")
	{
		textln("THIS MUST BE OVERRIDDEN!!!!");
	}
	
	OnAction(a_equip)
	{
		Call("EquipMe");
	}
	
	OnAction(a_use)
	{
		textln("You really need a target for this.");
	}

	OnAction(a_shoot)
	{
		textln("You really need a target for this. What're you gonna do, shoot yourself?");
	}

	OnActionWithOther(a_use)
	{
		textln("Might not be a good idea to shoot that.");
	}

	OnActionWithOther(a_shoot)
	{
		textln("Might not be a good idea to shoot that.");
	}
	
}

// Fists
// Technically not a weapon.
object o_fists named "fists", "fist" uses determiners "my"
{
	OnAction(a_examine)
	{
		if (player.berserk)
			textln("Who's a man and a half? YOU'RE a man and a half! A BERSERKER PACKIN' MAN AND A HALF!");
		else
			textln("Good ol' Lefty and Righty. Back on Earth, these are registered as lethal weapons.");
	}
}

// Pistol
object o_pistol : o_allweapons named "pistol" uses determiners "the", "my"
{
	init()
	{
		attackDice = 3;
		attackDiceSize = 6;
	}

	procedure("ExamineAlways")
	{
		textln("Your trusty sidearm. Standard issue. Never leave home without it!");
	}

	OnAction(a_take)
	{
		if (HasObject(player, o_pistol))
			textln("You already have this.");
		else
		{
			textln("You take the pistol.");
			GiveObject(player, o_pistol);
		}
	}

	onPlayerBrowse()
	{
		textln("A pistol.");
	}	
}

// Shotgun
object o_shotgun : o_allweapons named "shotgun" uses determiners "the", "my"
{
	procedure("EquipMe")
	{
	}

	procedure("ExamineFirst")
	{
		textln("It's a shotgun. It shoots things. Small things.");
		textln("Doesn't look very high-tech. Maybe UAC didn't try to reinvent the wheel?");
	}

	procedure("ExamineNext")
	{
		textln("It's a shotgun. It shoots things. Small things.");
	}
	
	OnAction(a_take)
	{
		if (HasObject(player, o_shotgun))
			textln("You already have this.");
		else
		{
			textln("You take the shotgun. A nice upgrade.");
			GiveObject(player, o_shotgun);
		}
	}

	onPlayerBrowse()
	{
		textln("A shotgun.");
	}
	
	onRoomBrowse()
	{
		textln("A shotgun is here on the ground.");
	}

}

// Rocket Launcher
object o_rocketlauncher : o_allweapons 
	named "rocket launcher", "rockets" uses determiners "the", "my"
{
	procedure("ExamineFirst")
	{
		textln("It's a rocket launcher. For bigger game and demons.");
		textln("When you need an explosion several feet away from you.");
	}

	procedure("ExamineNext")
	{
		textln("It's a rocket launcher. For bigger game and demons.");
	}
	
	OnAction(a_take)
	{
		if (HasObject(player, o_rocketlauncher))
			textln("You already have this.");
		else
		{
			textln("You take the rocket launcher. How can you hold all this stuff?");
			GiveObject(player, o_rocketlauncher);
		}
	}

	onPlayerBrowse()
	{
		textln("A rocket launcher.");
	}
	
	onRoomBrowse()
	{
		textln("A rocket launcher is on the wall.");
	}

}

// BFG!
object o_bfg : o_allweapons named "BFG", "BFG9000", "BFG 9000", "big fucking gun", "BFG9k" uses determiners "the", "my"
{
	procedure("ExamineAlways")
	{
		text("It's the BFG 9000. ");
		local x = random(5);
		if (x == 0)
			textln("Ohhhh yes.");
		else if (x == 1)
			textln("Hell yeah.");
		else if (x == 2)
			textln("Don't ask what it stands for.");
		else if (x == 3)
			textln("When something needs to be really, really dead.");
		else if (x == 4)
			textln("For the biggest of baddies and the quickest of paint jobs. Hope you like red.");
	}

	OnAction(a_take)
	{
		if (HasObject(player, o_bfg))
			textln("You already have this.");
		else
		{
			textln("You take the BFG 9000. A marine's best friend.");
			GiveObject(player, o_bfg);
		}
	}

	onPlayerBrowse()
	{
		text("A BFG 9000. ");
		local x = random(3);
		if (x == 0)
			textln("Nice.");
		else if (x == 1)
			textln("Sweet.");
		else if (x == 2)
			textln("Awesome.");
	}
	
	onRoomBrowse()
	{
		textln("Oh my God... is that a BFG?!");
	}

}
