
// ==========================
// Weapons
// ==========================

extend object o_weapon
{
	function equip()
	{
		textln("!!! o_allweapons:procedure(\"Equip\") must be overridden !!!");
	}
	
	override onAction(a_equip)
	{
		if (!hasObject(player, this))
			giveObject(player, this);
			
		if (player.equippedWeapon === identity(this))
			textln("You already have this equipped.");
		else
			equip();
	}
	
	override onAction(a_shoot)
	{
		addCue("info", "You really need a target for this. EQUIP it and SHOOT something else!");
	}

	onAction(a_use)
	{
		queueActionObject(a_equip, this);
	}

	onActionWithOther(a_use)
	{
		addCue("info", "You need to EQUIP a weapon, then SHOOT something!");
	}

}

// Fists
// Technically not a weapon.
extend object o_fists
{
	onAction(a_examine)
	{
		if (player.berserk)
			textln("Who's a man and a half? YOU'RE a man and a half! A BERSERKER PACKIN' MAN AND A HALF!");
		else
			textln("Good ol' Lefty and Righty. Back on Earth, these are registered as lethal weapons.");
	}
}

// Pistol
extend object o_pistol
{
	override function equip()
	{
		player.equippedWeapon = identity(this);
		player.equippedWeaponName = "pistol";
		player.equippedWeaponSound = "POP POP!";
		player.equippedAttackDice = 3;
		player.equippedAttackDiceSize = 6;
	}

	override function examineAlways()
	{
		textln("Your trusty sidearm. Standard issue. Never leave home without it!");
	}

	override function takeMe()
	{
		textln("You take the pistol.");
	}
	
	onPlayerBrowse()
	{
		textln("A pistol.");
	}	
}

// Shotgun
extend object o_shotgun
{
	override function equip()
	{
		player.equippedWeapon = identity(this);
		player.equippedWeaponName = "shotgun";
		player.equippedWeaponSound = "BLAM! *cha-chik*";
		player.equippedAttackDice = 10;
		player.equippedAttackDiceSize = 4;
	}
	
	override function examineFirst()
	{
		textln("It's a shotgun. It shoots things. Small things.");
		textln("Doesn't look very high-tech. Maybe UAC didn't try to reinvent the wheel?");
	}

	override function examineNext()
	{
		textln("It's a shotgun. It shoots things. Small things.");
	}
	
	override function takeMe()
	{
		textln("You take the shotgun.");
	}
	
	onPlayerBrowse()
	{
		textln("A shotgun.");
	}
	
	onRoomBrowse()
	{
		textln("A shotgun is here on the ground.");
	}

}

// Rocket Launcher
extend object o_rocketlauncher
{
	override function equip()
	{
		player.equippedWeapon = identity(this);
		player.equippedWeaponName = "rocket launcher";
		player.equippedWeaponSound = "Ka-FWOOMP!";
		player.equippedAttackDice = 5;
		player.equippedAttackDiceSize = 20;
	}

	override function examineFirst()
	{
		textln("It's a rocket launcher. For bigger game and demons.");
		textln("When you need an explosion several feet away from you.");
	}

	override function examineNext()
	{
		textln("It's a rocket launcher. For bigger game and demons.");
	}
	
	override function takeMe()
	{
		textln("You take the rocket launcher. How can you hold all this stuff?");
	}

	onPlayerBrowse()
	{
		textln("A rocket launcher.");
	}
	
	onRoomBrowse()
	{
		textln("A rocket launcher is on the wall.");
	}

}

// BFG!
extend object o_bfg
{
	override function equip()
	{
		player.equippedWeapon = identity(this);
		player.equippedWeaponName = "BFG 9000";
		player.equippedWeaponSound = "Chkk-ZOOWWWMMFFFPP!";
		player.equippedAttackDice = 20;
		player.equippedAttackDiceSize = 100;
	}

	override function examineAlways()
	{
		text("It's the BFG 9000. ");
		local x = irandom(5);
		if (x == 0)
			textln("Ohhhh yes.");
		else if (x == 1)
			textln("Hell yeah.");
		else if (x == 2)
			textln("Don't ask what it stands for.");
		else if (x == 3)
			textln("When something needs to be really, really dead.");
		else if (x == 4)
			textln("For the biggest of baddies and the quickest of paint jobs. Hope you like red.");
	}

	override function takeMe()
	{
		textln("You take the BFG 9000. A marine's best friend.");
	}
	
	onPlayerBrowse()
	{
		text("A BFG 9000. ");
		local x = irandom(3);
		if (x == 0)
			textln("Nice.");
		else if (x == 1)
			textln("Sweet.");
		else if (x == 2)
			textln("Awesome.");
	}
	
	onRoomBrowse()
	{
		textln("Oh my God... is that a BFG?!");
	}

}
