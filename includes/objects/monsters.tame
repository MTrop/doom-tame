
// ==========================
// Monsters
// ==========================

extend object o_monster
{
	override init()
	{
		examined = false;
	
		// name used for generic battle.
		name = "((MONSTER))";
		// max monster health (used for damage assessment).
		healthMax = 0;
		// max reaction time
		reactionMax = 0;

		health = 0;
		reaction = 0;
		dead = false;
		superdead = false;
	}
	
	// Called when "AI" ticks down a reaction.
	// Does not set anything. Just flavor.
	function monsterIdle()
	{
		// Do nothing. Override.
	}
	
	// Called when "AI" says to attack.
	// Should do something to the current player.
	function monsterAttack()
	{
		// Do nothing. Override.
	}

	// Called when "AI" says to attack, but player is already dead.
	// Does not set anything. Just flavor.
	function monsterAbortAttack()
	{
		// Do nothing. Override.
	}

	// Called when the monster dies.
	// Does not set anything. Just flavor.
	function monsterDie()
	{
		// Do nothing. Override.
	}

	// Called when the monster dies A LOT.
	// Does not set anything. Just flavor.
	function monsterSuperDie()
	{
		// Do nothing. Override.
	}

	// Called on monster examine if dead.
	// Does not set anything. Just flavor.
	function monsterStatusDead()
	{
		// Do nothing. Override.
	}
	
	// Called on monster examine if still alive.
	// Does not set anything. Just flavor.
	function monsterStatusAlive()
	{
		local percent = health * 100 / healthMax;
		if (percent < 10)
			textln("It looks like it's barely clinging to life.");
		else if (percent < 25)
			textln("It looks pretty darn wounded.");
		else if (percent < 50)
			textln("It looks significantly wounded.");
		else if (percent < 75)
			textln("It's wounded, but still kickin'!");
		else
			textln("It's practically unscathed.");
	}

	// Called when a monster is damaged. Sets "dead" state if it is dead.
	function checkMonsterDead()
	{
		local percent = health * 100 / healthMax;
		if (percent <= 0)
		{
			if (+percent >= 50)
			{
				MonsterSuperDie();
				superdead = true;
			}
			else
				MonsterDie();
			dead = true;
		}
	}
	
	// Called to reset reaction time.
	function resetReaction()
	{
		reaction = random(reactionMax / 2) + (reactionMax / 2);
	}

	override function examineAlways()
	{
		if (dead)
			MonsterStatusDead();
		else
			MonsterStatusAlive();
	}
	
	override onAction(a_take)
	{
		textln("You cannot take a monster. That's ludicrous.");
	}

	override onAction(a_shoot)
	{
		if (dead)
		{
			textln("It's already dead. Why waste the ammo?");
			end;
		}
		
		if (!player.equippedWeapon)
		{
			textln("I have nothing equipped. Maybe I could punch them?");
			end;
		}
	
		textln("You fire your "+player.equippedWeaponName+" at the "+name+"!");
		textln(player.equippedWeaponSound);
		
		local dmg = Math.rolldie(player.equippedAttackDiceSize, player.equippedAttackDice);
		health = health - dmg;
		textln("You deal "+dmg+" damage to the "+name+"!");
		
		checkMonsterDead();
	}

	// Called on monsters in a room.
	onAction(a_monsterthink)
	{
		if (dead)
			end;
	
		reaction = reaction - 1;
		if (reaction > 0)
			MonsterIdle();
		else
		{
			if (player.health > 0)
			{
				MonsterAttack();
				ResetReaction();
			}
			else
				MonsterAbortAttack();
		}
	}
	
	onAction(a_converse)
	{
		textln(
			"Oh, if only you could talk to these creatures, then perhaps you "+
			"could try and make friends with them, form alliances... now THAT would be interesting!"
		);
		textln("But you can't. Sorry.");
	}
	
	// Default Combat with fists.
	onAction(a_punch)
	{
		if (superdead)
		{
			local x = random(3);
			if (x == 0)
				textln("That thing's already a runny paste. I don't think it needs punching.");
			else if (x == 1)
				textln("You punch what's left of the "+name+". Yup. Still dead.");
			else if (x == 2)
				textln("Punch, punch, punch. You really need to get those aggressions out in a better way.");
			end;
		}

		if (dead)
		{
			local x = random(3);
			if (x == 0)
				textln("Stop! Stop! It's already dead!");
			else if (x == 1)
				textln("It's not getting any deader.");
			else if (x == 2)
				textln("Punching a corpse is easy on the knuckles, but aren't there better things to do?");
			end;
		}
		
		local dmg = 0;

		if (player.berserk)
		{
			local x = random(3);
			if (x == 0)
				textln("RRRGGGHHHH!!!! IT'S STUPID! IT'S GONNA BE STUPID AND DEAD!!");
			else if (x == 1)
				textln("HERE COMES THE NIGHT TRAIN!! CHOO-CHOO CHA-BOOGIE!");
			else if (x == 2)
				textln("KNOCK KNOCK! WHO'S THERE?! ME!! ME ME ME ME ME");
			else if (x == 2)
				textln("RIP AND TEAR! RIP AND TEAR YOUR GUTS!!");
				
			x = random(3);
			if (x == 0)
				textln("Ker-POOOWWWWWWW!");
			else if (x == 1)
				textln("POW POW POW!");
			else if (x == 2)
				textln("AAAA TATATATATATATATATATATATATA!!");

			dmg = 75 + random(50);
			
			health = health - dmg;
			text("You deal "+dmg+" damage to the "+name+"! ");

			x = random(2);
			if (x == 0)
				textln("RIP AND TEAR!");
			else if (x == 1)
				textln("OHHH YEAAAHHHH!!");
		}
		else
		{
			local x = random(3);
			if (x == 0)
				textln("You unleash a wimpy pummeling on the "+name+"!");
			else if (x == 1)
				textln("Here comes the airplane! If it were a fist! Look out, "+name+"!");
			else if (x == 2)
				textln("First the wind up, then the pitch! Take that, "+name+"!");
			
			x = random(3);
			if (x == 0)
				textln("BONK!");
			else if (x == 1)
				textln("BIFF!");
			else if (x == 2)
				textln("ZOK!");
			
			x = 3;
			while (x > 0)
			{
				dmg = dmg + (1 + random(4));
				x = x - 1;
			}
			
			health = health - dmg;
			text("You deal "+dmg+" damage to the "+name+"! ");

			x = random(3);
			if (x == 0)
				textln("Good for you!");
			else if (x == 1)
				textln("I'm sure it was worth it.");
			else if (x == 2)
				textln("Great job, kiddo.");
		}
		
		checkMonsterDead();
	}

}

// ==========================
// Zombie
// ==========================
extend object o_zombie
{
	override init()
	{
		examined = false;
		name = "zombie";
		healthMax = 20;
		health = 20;
		reactionMax = 5;
		reaction = 5;
	}
	
	override function examineFirst()
	{
		text("A hideous caricature of what once was a man (...or woman? Hard to tell) is ");
		if (superdead)
			textln("strewn about in bits and pieces all over the room.");
		else if (dead)
			textln("lying dead on the floor.");
		else
			textln("now shuffling aimlessly around the room.");
	}
	
	override function monsterIdle()
	{
		local x = random(3);
		if (x == 0)
			textln("The zombie mills about, as zombies do.");
		else if (x == 1)
			textln("The zombie shuffles around, and trips over itself.");
		else
			textln("The zombie mutters something in a demonic tongue. Maybe it was, \"brains?\"");
	}

	override function monsterAbortAttack()
	{
		textln("The zombie goes to attack, but notices that you look just as dead. Oh well.");
	}
	
	override function monsterAttack()
	{
		if (random(3) < 2) // 66% chance
		{
			textln("The zombie clumsily takes a swing at you!");
			if (random(4) < 3) // 75% chance
			{
				local dmg = random(5) + 5;
				textln("It slaps you for "+dmg+" damage! Eww!");
				player.health = player.health - dmg;
			}
			else // 25% chance
				textln("It misses, and twirls around due to momentum. Silly zombie!");
		}
		else // 33% chance
		{
			textln("The zombie lunges at you, growling menacingly!");
			if (random(5) < 4) // 80% chance
			{
				local dmg = random(10) + 10;
				textln("It bites you for "+dmg+" damage! Ouch!");
				player.health = player.health - dmg;
			}
			else // 20% chance
				textln("It misses, and falls to the ground.");
		}
		
	}
	
	override function monsterDie()
	{
		textln("The zombie screams and falls to the ground in a dull *thud*.");
		addObjectName(o_zombie, "dead zombie");

		textln("Huh. A red key fell to the ground.");
		giveObject(room, o_redkey);
	}

	override function monsterSuperDie()
	{
		textln("The zombie explodes into bloody bits. Damn, son! You really murdered it good!");
		addObjectName(o_zombie, "dead zombie");
		addObjectName(o_zombie, "bits of dead zombie");
		addObjectName(o_zombie, "bits of zombie");

		textln("Huh. A red key fell to the ground.");
		giveObject(room, o_redkey);
	}
	
	override function monsterStatusDead()
	{
		if (superdead)
			textln("Bits of what used to be a zombie are littered everywhere.");
		else
			textln("There's a dead zombie here.");
	}
	
	onRoomBrowse()
	{
		if (superdead)
			textln("Bits of splattered zombie are everywhere.");
		else if (dead)
			textln("There's a dead zombie, collapsed on the ground.");
		else
			textln("There's a zombie here.");
	}
	
}

extend object o_sergeant;
extend object o_imp;
extend object o_cacodemon;
extend object o_cyberdemon;


