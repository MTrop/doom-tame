
// ==========================
// Items
// ==========================

object archetype o_healthobject : o_allobjects
{
	// Called if healing is possible.
	function doHealing()
	{
		/* Do nothing. Override. */
	}
	
	onAction(a_use)
	{
		if (player.health >= 100)
			textln("You're at full health. You don't need to use this.");
		else
			DoHealing();
	}
	
	onActionWith(a_use, o_self)
	{
		info("Just USE this thing as-is.");
	}

	onActionWithOther(a_use)
	{
		textln("I should USE this on myself.");
	}

}

// Stimpak
object o_stimpak : o_healthobject named "stimpak" uses determiners "the"
{
	// --> Override
	function doHealing()
	{
		textln("You heal youself a bit.");
		removeObject(o_stimpak);
		player.health = min(player.health + 10, 100);
		queueAction(a_status);
	}

	function examineAlways()
	{
		textln("It's a small kit of adrenaline injections and bandages. Heals you a little.");
	}
	
	onAction(a_take)
	{
		if (hasObject(player, o_stimpak))
			textln("You already have this.");
		else
		{
			textln("You take the stimpak.");
			giveObject(player, o_stimpak);
		}
	}

	onRoomBrowse()
	{
		textln("There's a stimpak here on the floor.");
	}
	
	onPlayerBrowse()
	{
		textln("A stimpak.");
	}
	
}

// Medikit
object o_medikit : o_healthobject named "medikit" uses determiners "the"
{
	// --> Override
	function doHealing()
	{
		textln("You heal youself a bunch.");
		removeObject(o_medikit);
		player.health = min(player.health + 25, 100);
		queueAction(a_status);
	}

	function examineAlways()
	{
		textln("It's a large kit of gauze, stitching thread, forceps, and antiseptic. Heals you moderately.");
	}

	onAction(a_take)
	{
		if (hasObject(player, o_medikit))
			textln("You already have this.");
		else
		{
			textln("You take the medkit.");
			giveObject(player, o_medikit);
		}
	}

	onRoomBrowse()
	{
		textln("There's a medikit here on the floor.");
	}
	
	onPlayerBrowse()
	{
		textln("A medikit.");
	}

}

// Berserk Pack
object o_berserk : o_healthobject named "berserk", "berserk pack", "berserker", "berserker pack" uses determiners "the"
{
	// --> Override
	function doHealing()
	{
		textln("You heal youself completely, and you are now BERSERK! PUNCH THINGS!");
		removeObject(o_berserk);
		if (player.health < 100)
			player.health = 100;
		player.berserk = true;
		queueAction(a_status);
	}
	
	function examineAlways()
	{
		textln("It's a large kit of some kind of experimental drugs. Looks crazy.");
	}

	onAction(a_take)
	{
		if (hasObject(player, o_berserk))
			textln("You already have this.");
		else
		{
			textln("You take the berserk pack.");
			giveObject(player, o_berserk);
		}
	}

	// Override. Can always use.
	onAction(a_use)
	{
		DoHealing();
	}
	
}

// ==========================
// Keys
// ==========================

// All Keys
object archetype o_allkeys : o_allobjects
{
	onAction(a_use)
	{
		textln("This is better off used WITH something. Like a door. It's a key, after all.");
	}

}

// Red Key
object o_redkey : o_allkeys named "key", "keycard", "red key", "red keycard", "keycard with red markings"
{
	function examineFirst()
	{
		textln("It's a keycard with red markings on it. A little blood-stained, too. Though it doesn't matter - "+
		"it just makes it more red.");
	}

	function examineNext()
	{
		textln("It's a red keycard.");
	}
	
	onAction(a_take)
	{
		if (hasObject(player, o_redkey))
			textln("You already have this.");
		else
		{
			textln("You take the red keycard. Could be useful.");
			giveObject(player, o_redkey);
		}
	}

	onPlayerBrowse()
	{
		textln("A red keycard.");
	}

	onRoomBrowse()
	{
		textln("There's a keycard on the ground with red markings.");
	}
	
}

// Blue Key
object o_bluekey : o_allkeys named "key", "keycard", "blue key", "blue keycard" uses determiners "the"
{
	function examineFirst()
	{
		textln("It's a keycard with blue markings on it. Someone left it behind. Or dropped it. Who knows.");
	}

	function examineNext()
	{
		textln("It's a blue keycard.");
	}

	onAction(a_take)
	{
		if (hasObject(player, o_bluekey))
			textln("You already have this.");
		else
		{
			textln("You take the blue keycard. Why not?");
			giveObject(player, o_bluekey);
		}
	}

	onPlayerBrowse()
	{
		textln("A blue keycard.");
	}

	onRoomBrowse()
	{
		textln("There's a blue keycard on the floor.");
	}
	
}

// Yellow Key
object o_yellowkey : o_allkeys named "key", "keycard", "yellow key", "yellow keycard" uses determiners "the"
{
	function examineFirst()
	{
		textln("It's a keycard with yellow markings on it. Three guesses for what you do with it.");
	}

	function examineNext()
	{
		textln("It's a yellow keycard.");
	}

	onAction(a_take)
	{
		if (hasObject(player, o_yellowkey))
			textln("You already have this.");
		else
		{
			textln("You take the yellow keycard. Why not?");
			giveObject(player, o_yellowkey);
		}
	}

	onPlayerBrowse()
	{
		textln("A yellow keycard.");
	}

	onRoomBrowse()
	{
		textln("There's a yellow keycard on one of the shelves.");
	}
	
}
